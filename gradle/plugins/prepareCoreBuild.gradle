tasks.register('prepareCoreBuild') {
    doLast {
        copyLocalConfsIfNeeded()
    }
}

private void copyLocalConfsIfNeeded() {
    def localConfsFolder = new File(rootDir, "localConfFiles")
    def targetFiles = new ArrayList<File>()
    findLocalConfTargetFiles(rootDir, targetFiles)
    localConfsFolder.listFiles(new FileFilter() {
        @Override
        boolean accept(File pathname) {
            return pathname.isDirectory()
        }
    }).each { folder ->
        def destFolder = targetFiles.find { it.name == folder.name }
        if (destFolder == null) {
            throw new IllegalStateException("Error: folder ${folder.name} does not exist. You may need to do git submodule update --recursive")
        }
        def destConfFile = new File(destFolder, "local.conf")
        def confTextSb = new StringBuilder()
        if (folder.name == "planckJNIWrapper") {
            def prefix = new File(rootDir, "submodules/planckJNIWrapper/submodules/planckCoreV3/build-android").absolutePath
            confTextSb.append("PREFIX=${prefix}")
        }
        def confFiles = folder.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return name.endsWith(".conf")
            }
        })
        if (confFiles.length > 0) {
            confTextSb.append(System.lineSeparator())
            confTextSb.append(
                    confFiles.collect { confFile ->
                        confFile.text
                    }.join(System.lineSeparator())
            )
        }
        if (!confTextSb.isBlank() && (!destConfFile.exists() || confTextSb.toString() != destConfFile.text)) {
            destConfFile.write(confTextSb.toString())
        }
    }
}

private void findLocalConfTargetFiles(File rootFolder, ArrayList<File> targetFiles) {
    def submodulesFolder = new File(rootFolder, "submodules")
    if (!submodulesFolder.exists()) return
    submodulesFolder.listFiles(new FileFilter() {
        @Override
        boolean accept(File file) {
            return file.isDirectory() && file.name != "foldable-folder-list" && file.name != "libetpan"
        }
    }).each { currentFolder ->
        targetFiles.add(currentFolder)
        findLocalConfTargetFiles(currentFolder, targetFiles)
    }
}

preBuild.dependsOn(prepareCoreBuild)