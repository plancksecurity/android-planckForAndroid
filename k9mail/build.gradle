import com.android.build.OutputFile

def planckTestEmailAddress = "\"$System.env.PLANCK_TEST_EMAIL_ADDRESS\""
def planckTestEmailPassword = "\"$System.env.PLANCK_TEST_EMAIL_PASSWORD\""
def planckTestEmailServer = "\"$System.env.PLANCK_TEST_EMAIL_SERVER\""


def planckDragonEmail = "\"$System.env.PLANCK_DRAGON_EMAIL_ADDRESS\""
def planckDragonPassword = "\"$System.env.PLANCK_DRAGON_PASSWORD\""
def planckDragonServer = "\"$System.env.PLANCK_DRAGON_SERVER\""
def planckDragonUsername = "\"$System.env.PLANCK_DRAGON_USERNAME\""

buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: "com.android.application"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.google.dagger.hilt.android'

apply from: '../gradle/plugins/checkstyle-android.gradle'
apply from: '../gradle/plugins/utils/screenshots.gradle'
apply from: '../gradle/plugins/utils/appconfig-test.gradle'
apply from: '../gradle/plugins/utils/git-version.gradle'
apply from: '../gradle/plugins/utils/addaccount.gradle'
apply from: '../gradle/plugins/cucumber.gradle'
apply from: '../gradle/plugins/customTest.gradle'
apply from: '../gradle/plugins/prepareCoreBuild.gradle'
try {
    apply from: '../gradle/plugins/customConfig.gradle'
} catch(Exception ignored) {}

if (rootProject.testCoverage) {
    apply plugin: "jacoco"
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    //Required for certification; Only required in enterprise variants
    implementation "com.scottyab:rootbeer-lib:${versions.rootbeer}" //Used for root detection

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":planckJNIWrapper") //JNI wrapper to engine
    implementation project(':k9mail-library') //k9 native lib
    implementation project(':plugins:HoloColorPicker') //UI design
    implementation project(':plugins:openpgp-api-lib:openpgp-api') //Unsure
    implementation project(':foldableFolders') //UI design

    //Androidx Lib
    implementation "androidx.legacy:legacy-support-v4:${versions.legacySupport}" //androidx lib
    implementation "androidx.appcompat:appcompat:${versions.appCompat}" //androidx lib
    implementation "androidx.cardview:cardview:${versions.cardView}" //androidx lib
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerview}"
    implementation "androidx.vectordrawable:vectordrawable:${versions.vectordrawable}"
    implementation "androidx.webkit:webkit:1.4.0"
    implementation "androidx.preference:preference:${versions.preference}"
    implementation "androidx.work:work-runtime-ktx:${versions.workRuntime}"
    implementation "androidx.security:security-crypto:${versions.crypto}"
    implementation "androidx.activity:activity-ktx:${versions.androidxActivity}"
    implementation "androidx.fragment:fragment-ktx:${versions.androidxFragment}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "androidx.lifecycle:lifecycle-common:${versions.androidxLifecycle}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${versions.androidxLifecycle}"
    implementation "androidx.lifecycle:lifecycle-process:${versions.androidxLifecycle}"
    implementation "androidx.core:core-ktx:${versions.androidXcore}"
    implementation "androidx.test.espresso:espresso-idling-resource:${versions.espresso}"
    implementation "androidx.multidex:multidex:${versions.multidex}"

    implementation "net.openid:appauth:${versions.appauth}"
    implementation "com.auth0.android:jwtdecode:${versions.jwtDecode}" //OAuth, could likely be transformed into a small lib/file
    implementation "com.squareup.okio:okio:${versions.okio}" //IO Compliment
    implementation "commons-io:commons-io:${versions.commonsIo}" //Apache commons io
    implementation "com.google.android.material:material:${versions.material}"

    testImplementation "org.apache.ant:ant:${versions.ant}"

    implementation "com.squareup.moshi:moshi:${versions.moshi}"
    implementation "com.splitwise:tokenautocomplete:${versions.tokenAutoComplete}"
    implementation "com.github.amlcurran.showcaseview:library:${versions.showCaseView}"
    implementation "com.jakewharton.timber:timber:${versions.timber}"
    implementation "com.jakewharton:butterknife:${versions.butterKnife}" //UI Binding, should be able to upgrade
    implementation "com.github.pedrovgs:renderers:${versions.renderers}" //UI design
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    implementation "com.github.AppIntro:AppIntro:${versions.appIntro}"
    implementation "com.daimajia.swipelayout:library:${versions.swipeLayout}@aar" //UI design, albeit could be remade to our own lib
    implementation "com.karumi:dexter:${versions.dexter}" //Permissions, however it is archieved
    implementation "de.cketti.safecontentresolver:safe-content-resolver-v21:${versions.safeContentResolver}" //Attachment handler
    implementation "de.hdodenhof:circleimageview:${versions.circleImageView}"
    implementation "org.jsoup:jsoup:${versions.jsoup}" //Html parser
    implementation "com.takisoft.preferencex:preferencex:${versions.preferencesFix}"
    implementation "com.takisoft.preferencex:preferencex-ringtone:${versions.preferencesFix}"
    implementation "com.takisoft.preferencex:preferencex-datetimepicker:${versions.preferencesFix}"
    implementation "com.takisoft.preferencex:preferencex-colorpicker:${versions.preferencesFix}"



    // Arch dependencies
    implementation "com.google.dagger:hilt-android:${versions.hilt}"
    kapt "com.google.dagger:hilt-compiler:${versions.hilt}"
    implementation "com.github.lisawray.groupie:groupie:${versions.groupie}"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:${versions.groupie}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinCoroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoroutines}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlinCoroutines}"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.kotlinSerialization}"

    implementation "net.sf.biweekly:biweekly:${versions.biweekly}"
    implementation 'org.minidns:minidns-hla:1.0.4'

    //implementation "androidx.annotation:annotation:1.1.0"
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation "androidx.test.uiautomator:uiautomator:${versions.uiAutomator}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${versions.espresso}"
    androidTestImplementation ("androidx.test.espresso:espresso-core:${versions.espresso}") {
        exclude module: 'support-annotations'
    }

    testImplementation "org.robolectric:robolectric:${versions.robolectric}"
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
    testImplementation "org.mockito:mockito-inline:${versions.mockito}"
    testImplementation "org.mockito.kotlin:mockito-kotlin:${versions.mockitoKotlin}"
    testImplementation "io.mockk:mockk:${versions.mockk}"
    testImplementation "org.jdom:jdom2:${versions.jdom}"
    testImplementation "androidx.arch.core:core-testing:2.2.0"

    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:${versions.espresso}"

    androidTestImplementation("androidx.test.espresso:espresso-contrib:${versions.espresso}") {
        exclude module: "protobuf-lite"
    }
    testImplementation "androidx.test:core:${versions.androidXcoreTest}"
    testImplementation "androidx.test.ext:junit:${versions.androidXjunit}"
    androidTestImplementation "androidx.test.ext:junit:${versions.androidXjunit}"
    testImplementation "androidx.test:rules:${versions.runner_rules}"
    testImplementation "androidx.test:runner:${versions.runner_rules}"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "com.google.truth:truth:${versions.truth}"
    testImplementation "org.jdom:jdom2:${versions.jdom}"
    testImplementation "org.robolectric:shadows-multidex:${versions.robolectric}"

    androidTestImplementation "org.mockito:mockito-android:${versions.mockito}"

    // Cucumber
/*    androidTestImplementation('info.cukes:cucumber-android:1.2.4') {
        exclude module: 'cucumber-jvm-deps'
    }

    androidTestImplementation('info.cukes:cucumber-picocontainer:1.2.4') {
        exclude module: 'cucumber-jvm-deps'
    }
    androidTestImplementation 'info.cukes:cucumber-jvm-deps:1.0.5'*/

    /*androidTestImplementation group: 'io.cucumber', name: 'cucumber-java', version: '4.2.3'
    androidTestImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '4.2.3'*/

    androidTestImplementation "io.cucumber:cucumber-android:${versions.cucumber}"
    androidTestImplementation "io.cucumber:cucumber-picocontainer:${versions.cucumber_pico}"

    // Screenshots
    androidTestImplementation 'com.squareup.spoon:spoon-client:1.7.1'
    androidTestImplementation('com.schibsted.spain:barista:2.8.0') {
        exclude group: 'com.android.support'
        //exclude group: 'org.jetbrains.kotlin' // Only if you already use Kotlin in your project
    }
    kaptAndroidTest "com.google.dagger:hilt-compiler:${versions.hilt}"
}

kapt {
    correctErrorTypes true
}

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "${appId}"
        versionName "${gitVersionName}"
        versionCode buildConfig.versionCode
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        setProperty("archivesBaseName", "${gitVersionName}-${versionCode}")
        generatedDensities = ["mdpi", "hdpi", "xhdpi"]

        testApplicationId "com.fsck.k9.planck.ui.activities.test"
        testInstrumentationRunner "com.fsck.k9.planck.ui.activities.test.CucumberTestCase"
        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "PLANCK_TEST_EMAIL_ADDRESS", "${planckTestEmailAddress}"
        buildConfigField "String", "PLANCK_TEST_EMAIL_PASSWORD", "${planckTestEmailPassword}"
        buildConfigField "String", "PLANCK_TEST_EMAIL_SERVER", "${planckTestEmailServer}"

        buildConfigField "String", "PLANCK_DRAGON_EMAIL_ADDRESS", "${planckDragonEmail}"
        buildConfigField "String", "PLANCK_DRAGON_PASSWORD", "${planckDragonPassword}"
        buildConfigField "String", "PLANCK_DRAGON_SERVER", "${planckDragonServer}"

        buildConfigField "String", "PLANCK_DRAGON_USERNAME", "${planckDragonUsername}"
        buildConfigField "String", "BASE_VERSION", "\"${gitVersionName}\""

        buildConfigField "boolean", "USE_FAKE_RESTRICTIONS_MANAGER", "${project.findProperty("useFakeManager") ?: true}"
        buildConfigField "int", "USER", "0"
        testBuildType project.findProperty("testBuildType") ?: "debug"
    }

    signingConfigs {
        endUserRelease {
            if (project.hasProperty("keyAlias")) { keyAlias = project.keyAlias }
            if (project.hasProperty("keyPassword")) { keyPassword = project.keyPassword }
            if (project.hasProperty("storeFile")) { storeFile = file(project.storeFile) }
            if (project.hasProperty("storePassword")) { storePassword = project.storePassword }
        }
        enterpriseRelease {
            if (project.hasProperty("enterpriseKeyAlias")) { keyAlias = project.enterpriseKeyAlias }
            if (project.hasProperty("enterpriseKeyPassword")) { keyPassword = project.enterpriseKeyPassword }
            if (project.hasProperty("enterpriseStoreFile")) { storeFile = file(project.enterpriseStoreFile) }
            if (project.hasProperty("enterpriseStorePassword")) { storePassword = project.enterpriseStorePassword }
        }
        demo {
            keyAlias = project.findProperty("demoKeyAlias")
            keyPassword = project.findProperty("demoKeyPassword")
            storeFile = project.findProperty("demoStoreFile")
            storePassword = project.findProperty("demoStorePassword")
        }
    }

    flavorDimensions "type", "store"

    productFlavors {

        enterprise {
            buildConfigField "boolean", "IS_ENTERPRISE", "true"
            versionNameSuffix ".enterprise"
            dimension "type"
            applicationIdSuffix ".enterprise"
        }

        play {
            buildConfigField "boolean", "IS_ENTERPRISE", "true"
            versionNameSuffix ".play"
            dimension "type"
            applicationIdSuffix ".play"
        }

        if (isEndUserFlavorEnabled()) {
            endUser {
                buildConfigField "boolean", "IS_ENTERPRISE", "false"
                dimension "type"
            }
        }

        if (hasDemoProperties()) {
            demo {
                buildConfigField "boolean", "IS_ENTERPRISE", "true"
                versionNameSuffix ".enterprise" + demoAppVersionNameSuffix
                dimension "type"
                applicationIdSuffix ".enterprise" + demoAppIdSuffix
                versionCode project.findProperty("demoVersionCode") ?: buildConfig.versionCode
            }
        }

        playStore {
            dimension "store"
        }

        fDroid {
            dimension "store"
        }

    }

    variantFilter { variant ->
        def names = variant.getFlavors()*.name
        if ((names.contains('enterprise') || names.contains('play')) && names.contains('fDroid')) {
            variant.setIgnore(true)
        }
        if (hasDemoProperties()) {
            def type = variant.getBuildType().name
            if (names.contains('demo') && (names.contains('fDroid') || type != 'release')) {
                variant.setIgnore(true)
            }
        }
    }

    buildTypes {
        release {
            if (project.hasProperty("storeFile")) {
                if (isEndUserFlavorEnabled()) {
                    productFlavors.endUser.signingConfig signingConfigs.endUserRelease
                }
                productFlavors.play.signingConfig signingConfigs.endUserRelease
            }
            if (project.hasProperty("enterpriseStoreFile")) {
                productFlavors.enterprise.signingConfig signingConfigs.enterpriseRelease
            }
            if (hasDemoProperties()) {
                productFlavors.demo.signingConfig signingConfigs.demo
            }

            buildConfigField "boolean", "DEVELOPER_MODE", "false"
            buildConfigField "boolean", "USE_PASSPHRASE_FOR_NEW_KEYS", "false"
        }

        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled rootProject.testCoverage

            buildConfigField "boolean", "DEVELOPER_MODE", "true"
            buildConfigField "boolean", "USE_PASSPHRASE_FOR_NEW_KEYS", "${project.findProperty("DEBUG_USE_PASSPHRASE_FOR_NEW_KEYS") ?: false}"
        }
    }
    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
        lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }
    packagingOptions {
        exclude "META-INF/DEPENDENCIES.txt"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/dependencies.txt"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/LICENSE"
        exclude "META-INF/license.txt"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/notice.txt"
        exclude "META-INF/LGPL2.1"
        exclude "META-INF/README"
        exclude "LICENSE.txt"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    productFlavors {
    }

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a', 'armeabi-v7a', "x86", "x86_64"
            universalApk true
        }
    }

    applicationVariants.all { variant ->
        defineOAuthConfigs(variant)
        variant.outputs.each { output ->
            def baseAbiVersionCode = abiCodes.get(output.getFilter(OutputFile.ABI))
            if (baseAbiVersionCode != null) {
                output.versionCodeOverride = baseAbiVersionCode * 10000000 + variant.versionCode
            }
        }
    }

    sourceSets {
        test.manifest.srcFile "src/test/AndroidManifest.xml"
        androidTest {
            manifest.srcFile "src/androidTest/AndroidManifest.xml"
            assets.srcDirs = ['src/androidTest/assets']
        }
        if (hasDemoProperties()) {
            demo {
                res.srcDirs += ['src/enterprise/res']
                java.srcDirs += ['src/enterprise/java']
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        viewBinding = true
    }

}

private static void defineOAuthConfigs(variant) {
    def mergedFlavor = variant.getMergedFlavor()
    def oauthAppId = variant.applicationId
    if (variant.applicationId.contains("security.planck") && variant.applicationId.contains("enterprise.debug")) {
        oauthAppId = "security.planck.enterprise.debug"
    } else if (variant.applicationId.contains("security.planck") && variant.applicationId.contains("enterprise")) {
        oauthAppId = "security.planck.enterprise"
    }
    variant.buildConfigField "String", "OAUTH_APP_ID", "\"$oauthAppId\""
    mergedFlavor.manifestPlaceholders["appAuthRedirectScheme"] = oauthAppId
    mergedFlavor.manifestPlaceholders["oAuthAppId"] = oauthAppId
    //noinspection GroovyFallthrough
    switch (variant.name) {
        case "enterprisePlayStoreRelease":
        case "demoPlayStoreRelease":
            variant.buildConfigField "String", "OAUTH_GMAIL_CLIENT_ID", "\"509355171185-oojhqep2loqb8022s1kged0rf29uudhk.apps.googleusercontent.com\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_CLIENT_ID", "\"d84fd8f6-feea-460e-aef6-f9162a6d670d\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_REDIRECT_URI", "\"msauth://security.planck.enterprise/%2B3NsYzN%2Fk%2Fj11m2ClpTTuvKL2eY%3D\""
            break
        case "enterprisePlayStoreDebug":
            variant.buildConfigField "String", "OAUTH_GMAIL_CLIENT_ID", "\"509355171185-qs9e78n1bkd9n872h8k2i506278grmpd.apps.googleusercontent.com\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_CLIENT_ID", "\"dedb9d38-8697-44a4-9bcc-8b8b35c5b3af\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_REDIRECT_URI", "\"msauth://security.planck.enterprise.debug/qpdLrCNt8%2F00V8s47FRpWhDdF9A%3D\""
            break
        case "playPlayStoreRelease":
            variant.buildConfigField "String", "OAUTH_GMAIL_CLIENT_ID", "\"509355171185-298svmr5klnl9dpdi655vjsn3d13miob.apps.googleusercontent.com\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_CLIENT_ID", "\"59a25209-d35b-48c9-8a76-fc71a37da409\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_REDIRECT_URI", "\"msauth://security.planck.play/Y8FGuyTMrOpTVHbC%2B%2BoQ8JbY09g%3D\""
            break
        case "playPlayStoreDebug":
            variant.buildConfigField "String", "OAUTH_GMAIL_CLIENT_ID", "\"509355171185-ckb8btb852h78k08jqsvlho1t8dmhv3o.apps.googleusercontent.com\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_CLIENT_ID", "\"fb1f8b8e-aba1-4395-9675-d796631eb80e\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_REDIRECT_URI", "\"msauth://security.planck.play.debug/qpdLrCNt8%2F00V8s47FRpWhDdF9A%3D\""
            break
        default:
            variant.buildConfigField "String", "OAUTH_GMAIL_CLIENT_ID", "\"\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_CLIENT_ID", "\"\""
            variant.buildConfigField "String", "OAUTH_MICROSOFT_REDIRECT_URI", "\"\""
            break
    }
}

afterEvaluate {
    if (hasDemoProperties()) {
        tasks.findAll { task ->
            task.name.contains("Demo") && (task.name.contains("test") || task.name.contains("lint"))
        }.forEach {
            it.enabled = false
        }
    }
    configureSequoiaLogging()
    configureCoreLogging()
}

private void configureSequoiaLogging() {
    if (gradle.startParameter.taskNames.every { it.contains("Debug") }) {
        if (project.findProperty("ENABLE_SEQUOIA_LOGS") ?: false) {
            rootProject.ext["SEQUOIA_TARGET"] = "debug"
        }
    }
}

private void configureCoreLogging() {
    if (gradle.startParameter.taskNames.every { it.contains("Debug") }) {
        if (project.findProperty("ENABLE_CORE_LOGS") ?: false) {
            rootProject.ext["CORE_LOG"] = "-DPEP_LOG"
        }
    }
}

private boolean hasDemoProperties() {
    return project.hasProperty("demoAppVersionNameSuffix") &&
            project.hasProperty("demoAppIdSuffix")
}

private boolean isEndUserFlavorEnabled() {
    return project.findProperty("enableEndUser") ?: false
}
