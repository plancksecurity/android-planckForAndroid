apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'


apply from: '../gradle/plugins/checkstyle-android.gradle'

if (rootProject.testCoverage) {
    apply plugin: 'jacoco'
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    api 'org.apache.james:apache-mime4j-core:0.7.2'
    api 'org.apache.james:apache-mime4j-dom:0.7.2'
    implementation "com.squareup.okio:okio:${versions.okio}"
    implementation "commons-io:commons-io:${versions.commonsIo}"
    implementation 'com.jcraft:jzlib:1.0.7'
    implementation 'com.beetstra.jutf7:jutf7:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation "com.jakewharton.timber:timber:${versions.timber}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'com.madgag.spongycastle:pg:1.51.0.0'

    testImplementation "com.squareup.okio:okio:${versions.okio}"
    testImplementation "org.robolectric:robolectric:${versions.robolectric}"
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "com.google.truth:truth:${versions.truth}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"

    // The Android Gradle plugin doesn't seem to put the Apache HTTP Client on the runtime classpath anymore when
    // running JVM tests.
    testImplementation "org.apache.httpcomponents:httpclient:4.5.5"
}

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        minSdkVersion buildConfig.minSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // for using Apache HTTP Client
    useLibrary 'org.apache.http.legacy'

    buildTypes {
        debug {
            testCoverageEnabled rootProject.testCoverage
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }
}
